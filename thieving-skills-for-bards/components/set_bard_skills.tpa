///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Sets Bard Thieving Skills                                             //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Move Over Required Files                                              //
///////////////////////////////////////////////////////////////////////////
COPY ~thieving-skills-for-bards/spl/MO#BSK01.SPL~    ~override~ // Assigns thief skill points to bards
COPY ~thieving-skills-for-bards/lua/M_MOTHIE.lua~    ~override~ // Lua file read by EEex, provided by Bubbs and further edited by myself

///////////////////////////////////////////////////////////////////////////
// Modify Base Bard CLAB File to Assign Thief Skills Points              //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLABBA01.2DA~ override
  LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 20 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK01 END
  PRETTY_PRINT_2DA
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Set Max Skills Points in THIEFSCL.2da for Base Bard                   //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~THIEFSCL.2DA~ ~override~
  SET_2DA_ENTRY 3 5 1 ~100~   // PICK_POCKETS
  SET_2DA_ENTRY 4 5 1 ~100~   // OPEN_LOCKS
  SET_2DA_ENTRY 5 5 1 ~100~   // FIND_TRAPS
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Remove 1st Level Skill Bonus for Base Bard                            //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~CLASISKL.2DA~ ~override~
  SET_2DA_ENTRY 3 5 1 ~0~   // PICK_POCKETS
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Set Standard Bard Pick Pocket Skill Progression to 0                  //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~SKILLBRD.2DA~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (index = 3 ; index < rows ; ++index) BEGIN
    SET_2DA_ENTRY index 1 1 ~0~
  END
  PRETTY_PRINT_2DA
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Set Skills for ALL Bard Kits                                          //
///////////////////////////////////////////////////////////////////////////
 COPY_EXISTING ~KITLIST.2DA~ ~override~
  READ_2DA_ENTRIES_NOW ~bard_kitlist~ 10
  FOR (row = 3; row < bard_kitlist; row += 1) BEGIN
  READ_2DA_ENTRY_FORMER ~bard_kitlist~ row 5 kit_clab
  READ_2DA_ENTRY_FORMER ~bard_kitlist~ row 1 kit_label
  READ_2DA_ENTRY_FORMER ~bard_kitlist~ row 8 kit_class
  
  PATCH_IF (%kit_class% = 5) BEGIN
    // Modify Bard CLAB File to Assign Thief Skills Points Every Level
    PATCH_IF FILE_EXISTS_IN_GAME ~%kit_clab%.2da~ BEGIN
      INNER_ACTION BEGIN
      COPY_EXISTING ~%kit_clab%.2DA~ override
        LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 20 f_Increment = 1 STR_VAR f_Entry = AP_MO#BSK01 END
        PRETTY_PRINT_2DA
      BUT_ONLY
      END
    END

    // Set Max Skills Points in THIEFSCL.2DA
    INNER_ACTION BEGIN
    COPY_EXISTING ~THIEFSCL.2DA~ ~override~
      READ_2DA_ENTRIES_NOW thiefscl 3
      COUNT_2DA_COLS cols
      FOR (col = 0; col < cols - 1; ++col) BEGIN
      READ_2DA_ENTRY_FORMER thiefscl 0 col column_name
      PATCH_IF "%column_name%" STRING_EQUAL_CASE "%kit_label%" BEGIN
        SET_2DA_ENTRY 3 (col + 1) 1 ~100~   // PICK_POCKETS
        SET_2DA_ENTRY 4 (col + 1) 1 ~100~   // OPEN_LOCKS
        SET_2DA_ENTRY 5 (col + 1) 1 ~100~   // FIND_TRAPS
      END
      END
    BUT_ONLY
    END

    // Remove 1st Level Pick Pocket Bonus from ALL Bard Kits
    INNER_ACTION BEGIN
    COPY_EXISTING ~CLASISKL.2DA~ ~override~
      READ_2DA_ENTRIES_NOW clasiskl 3
      COUNT_2DA_COLS cols
      FOR (col = 0; col < cols - 1; ++col) BEGIN
      READ_2DA_ENTRY_FORMER clasiskl 0 col column_name
      PATCH_IF "%column_name%" STRING_EQUAL_CASE "%kit_label%" BEGIN
        SET_2DA_ENTRY 3 (col + 1) 1 ~0~   // PICK_POCKETS
      END
      END
    BUT_ONLY
    END
  END
  END
